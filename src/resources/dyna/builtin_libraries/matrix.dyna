
multiply(A,B) = matrix {
    elem(X,Y) += A.elem(X,Z) * B.elem(Z,Y).
}.

add(A, B) = matrix {
    elem(X,Y) += A.elem(X,Y) ; B.elem(X,Y).
}.

transpose(A) = matrix {
    elem(X,Y) = A.elem(Y,X).
}.

matrix(M) = {
    elem(X,Y) = M.elem(X,Y).
    transpose = transpose($self).
    add(Other) = add($self, Other).
    multiply(Other) = multiply($self,Other).

    row(X) :- _ = $self.elem(X, _).
    col(Y) :- _ = $self.elem(_, Y).
    rows += 1 for $self.row(_).
    cols += 1 for $self.col(_).
}.

zero_matrix(A,B) = matrix {
    elem(X:range(A), Y:range(B)) = 0.
}.

identity_matrix(I) = matrix {
    elem(A,A) += 1 for A:range(I).
    elem(A:range(I), B:range(I)) += 0.
}.



% parameter_matrix(Name) = matrix {
%                              elem(X,Y) = $parameter(&matrix(Name,X,Y)).
%                          }.

%:- export parameter_matrix/1.



:- export matrix/1.
:- export zero_matrix/2.
:- export identity_matrix/1.